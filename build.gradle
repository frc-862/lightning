buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "edu.wpi.first:GradleRIO:2021.3.1"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

ext {
    mGroupID = "com.lightningrobotics"
    mArtifactID = "common"
    mVersionName = System.getenv("VERSION")
}

def gradleRioPluginId = 'edu.wpi.first.GradleRIO'
final gradleRioInstalled = project.getPlugins().hasPlugin(gradleRioPluginId)
if(gradleRioInstalled) {
    final Plugin plugin = project.getPlugins().getPlugin(gradleRioPluginId)
    println 'Plugin already applied'
} else {
    apply plugin: "edu.wpi.first.GradleRIO"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    // TODO need to make this dependable on the implementing project
    implementation wpi.deps.wpilib()
    nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)
    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, false)
    compile group: 'org.ejml', name: 'ejml-all', version: '0.40'
}

publishing {

    publications {
        gpr(MavenPublication) {
            groupId mGroupID
            artifactId mArtifactID
            version mVersionName
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/frc-862/lightning")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    
}